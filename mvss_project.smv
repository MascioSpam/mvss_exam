------------------------
-- MVSS Project       --
-- Flavio Mascetti    --
-- Student ID #262277 --
------------------------

-----------------------------------------------------------------------------
-- Model Checking:							   --
-- Handling Zero-Priced Goods of NetBill Security and Transaction Protocol --
-----------------------------------------------------------------------------

------------------------------------------------------
-- Protocol specifications:                         --
-- http://www.sti.uniurb.it/aldini/mvss/2016s2a.pdf --
------------------------------------------------------

-------------
-- Modules --
-------------

-- List of Zero-Priced Goods protocol types
--- 1: Certified Delivery
--- 2: Certified Delivery without NetBill Server
--- 3: Verified Delivery
--- 4: Unverified Delivery

MODULE main
 VAR
   c : consumer (m);
   m : merchant (c, n);
   n : netbill (m);

-- Checking that all computation end whit "Purchase Done"
SPEC AF (c.state = PD & m.state = PD & (n.state = PD | n.state = I));


-- List of consumer states:
--- I:	  Idle
--- PR:   Price Request
--- SEPO: Signed Electronic Payment Order
--- PD:   Purchase done

MODULE consumer(m)
 VAR
   state: {I, PR, SEPO, PD};
   type : {1, 2, 3, 4};
 
 ASSIGN
   init (type) := {1, 2, 3, 4};
   next (type) := type;

   init (state) := I;
   next (state) := case
     state = I : PR;
   -- Type 1/2/3
     state = PR & m.state = GE : SEPO;
     state = SEPO & m.state = SR : PD;
   -- Type 4
     state = PR & m.state = SR : PD;
     TRUE : state;
   esac;


-- List of merchant states:
--- I:    Idle
--- GE:   Encrypted Goods
--- EEPO: Endorsed EPO
--- SR:   Signed Result
--- PD:   Purchase done

MODULE merchant(c, n)
 VAR
  state: {I, GE, EEPO, SR, PD};
 
 ASSIGN
   init (state) := I;
   next (state) := case
     state = I & c.state = PR & c.type != 4 : GE;
   -- Type 1
     state = GE & c.state = SEPO & c.type = 1 : EEPO;
     state = EEPO & n.state = SR & c.type = 1 : SR;
   -- Type 2/3
     state = GE & c.state = SEPO & (c.type = 2 | c.type = 3) : SR;
   -- Type 4
     state = I & c.state = PR & c.type = 4 : SR;
   -- Purchase Done
     state = SR & c.state = PD : PD;
     TRUE : state;
   esac;


-- List of netbill server states:
--- I:  Idle
--- SR: Signed Result
--- PD:   Purchase done

MODULE netbill(m)
 VAR
  state: {I, SR, PD};
 
 ASSIGN
   init (state) := I;
   next (state) := case
     state = I & m.c.type = 1 & m.state = EEPO : SR;
   -- Purchase Done
     state = SR & m.state = PD : PD;
     TRUE : state;
   esac;
